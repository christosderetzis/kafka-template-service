plugins {
    id 'jacoco'
}

dependencies {
    implementation project(':common-module')
    implementation 'net.datafaker:datafaker:2.5.3'
    testImplementation project(':common-module').sourceSets.test.output

    // postgresql + jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
}

// Functional tests configuration
sourceSets {
    functionalTest {
        java {
            srcDir 'src/functionalTest/java'
        }
        resources {
            srcDir 'src/functionalTest/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
    functionalTestCompileOnly.extendsFrom testCompileOnly
    functionalTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task functionalTest(type: Test) {
    description = 'Runs functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    shouldRunAfter test

    useJUnitPlatform()
}

// JaCoCo configuration
jacoco {
    toolVersion = '0.8.13'
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/config/**',
                '**/entity/**',
                '**/repository/**',
                '**/mapper/**',
                '**/ConsumerModuleApplication.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            excludes = [
                '*.config.*',
                '*.entity.*',
                '*.repository.*',
                '*.mapper.*',
                '*.ConsumerModuleApplication'
            ]
        }
    }
}

test {
    finalizedBy jacocoTestReport
}

check {
    dependsOn jacocoTestCoverageVerification
}

tasks.named('processFunctionalTestResources') {
    duplicatesStrategy = 'include'
}

processFunctionalTestResources {
    duplicatesStrategy = 'include'
}
